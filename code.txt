#include <WiFi.h>
#include <Firebase_ESP_Client.h>
#include <DHT.h>

const char *ssid = "Gsoft";
const char *password = "amirrahiii";

#define API_KEY "AIzaSyBn6JN3IH68U_elgi9pgYAdPEO6659FKiM"
#define DATABASE_URL "https://nodemcu-gpstracker-default-rtdb.firebaseio.com/"

#define DHTPIN 4             // Humidity and temperature sensor pin
#define DHTTYPE DHT11        // DHT 11
#define SMOKE_SENSOR_PIN 34  // Smoke sensor analog pin

DHT dht(DHTPIN, DHTTYPE);

FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;
bool signupOK = false;

void setupWiFi() {
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Connecting to WiFi...");
  }
  Serial.println("Connected to WiFi");
}

void setupFirebase() {
  config.api_key = API_KEY;
  config.database_url = DATABASE_URL;

  if (Firebase.signUp(&config, &auth, "", "")) {
    Serial.println("Firebase Sign Up Successful");
    signupOK = true;
  } else {
    Serial.printf("Firebase Sign Up Failed. Error: %s\n", config.signer.signupError.message.c_str());
  }

  Firebase.begin(&config, &auth);
  Firebase.reconnectWiFi(true);
}

void setup() {
  Serial.begin(115200);

  setupWiFi();      // Connect to WiFi
  setupFirebase(); // Initialize Firebase

  // Initialize DHT sensor
  dht.begin();
}

void loop() {
  delay(3000);

  // Read temperature and humidity
  float humidity = dht.readHumidity();
  float temperature = dht.readTemperature();

  // Read analog output from smoke sensor
  int smokeValue = analogRead(SMOKE_SENSOR_PIN);
  Serial.print("Smoke Sensor Analog Value: ");
  Serial.println(smokeValue);

  // Save sensor data to Firebase
  if (signupOK) {
    // Save humidity data
    if (Firebase.RTDB.setFloat(&fbdo, "sensors/humidity", humidity)) {
      Serial.println("Humidity data saved to Firebase successfully.");
    } else {
      Serial.println("Failed to save humidity data to Firebase.");
      Serial.println("HTTP response code: " + String(fbdo.httpCode()));
      Serial.println("Error: " + fbdo.errorReason());
    }

    // Save temperature data
    if (Firebase.RTDB.setFloat(&fbdo, "sensors/temperature", temperature)) {
      Serial.println("Temperature data saved to Firebase successfully.");
    } else {
      Serial.println("Failed to save temperature data to Firebase.");
      Serial.println("HTTP response code: " + String(fbdo.httpCode()));
      Serial.println("Error: " + fbdo.errorReason());
    }

    // Save smoke sensor data
    if (Firebase.RTDB.setInt(&fbdo, "sensors/smoke", smokeValue)) {
      Serial.println("Smoke sensor data saved to Firebase successfully.");
    } else {
      Serial.println("Failed to save smoke sensor data to Firebase.");
      Serial.println("HTTP response code: " + String(fbdo.httpCode()));
      Serial.println("Error: " + fbdo.errorReason());
    }
  } else {
    Serial.println("Firebase Sign Up failed. Cannot save sensor data.");
  }
}
